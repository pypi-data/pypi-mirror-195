Value Required INTERFACE (\S+)
Value STATE (\S+)
Value PROTOCOL_STATE (\S+)
Value DESCRIPTION (.*)
Value BANDWIDTH (\d+)
Value MTU (\d+)
Value IP_ADDRESS ((\d{1,3}\.){3}\d{1,3})
Value IP_ADDRESS_MASK (\d+)
Value SPEED (\S+)
Value DUPLEX (\S+)
Value LINK_TYPE (\S+)
Value CLEAR_COUNTERS (.*)
Value LAST_300_SEC_INPUT_RATE_BYTES (\S+)
Value LAST_300_SEC_INPUT_RATE_BITS (\S+)
Value LAST_300_SEC_INPUT_RATE_PACKETS (\S+)
Value LAST_300_SEC_OUTPUT_RATE_BYTES (\S+)
Value LAST_300_SEC_OUTPUT_RATE_BITS (\S+)
Value LAST_300_SEC_OUTPUT_RATE_PACKETS (\S+)
Value INPUT_PACKETS (\d+)
Value INPUT_BTYPES (\d+)
Value INPUT_BUFFERS (\d+)
Value INPUT_BROADCASTS (\d+)
Value INPUT_MULTICASTS (\d+)
Value INPUT_PAUSES (\d+)
Value INPUT_ERRORS (\d+)
Value INPUT_RUNTS (\d+)
Value INPUT_GIANTS (\d+)
Value INPUT_CRC (\d+|-)
Value INPUT_ALIGN_ERRORS (\d+|-)
Value INPUT_OVERRUNS (\d+|-)
Value INPUT_DRIBBLES (\d+|-)
Value INPUT_DROPS (\d+|-)
Value INPUT_FRAME_ERRORS (\d+|-)
Value INPUT_NO_BUFFERS (\d+|-)
Value OUTPUT_PACKETS (\d+)
Value OUTPUT_BTYPES (\d+)
Value OUTPUT_BUFFERS (\d+|-)
Value OUTPUT_BROADCASTS (\d+)
Value OUTPUT_MULTICASTS (\d+)
Value OUTPUT_PAUSES (\d+|-)
Value OUTPUT_ERRORS (\d+|-)
Value OUTPUT_UNDERRUNS (\d+|-)
Value OUTPUT_COLLISIONS (\d+|-)
Value OUTPUT_DEFERRED (\d+|-)
Value OUTPUT_LOST_CARRIERS (\d+|-)

Start
  # Ethernet0/0 current state: UP
  ^\S+\s+current\s+state: -> Continue.Record
  ^${INTERFACE}\s+current\s+state:\s+${STATE} -> Raw_1
  # GigabitEthernet0/0
  # Current state: UP
  ^\S+$$ -> Continue.Record
  ^${INTERFACE}$$
  ^Current\s+state:\s+${STATE} -> Raw_2

Raw_1
  ^Line\s+protocol\s+current\s+state:\s+${PROTOCOL_STATE}
  ^Description:\s+${DESCRIPTION}
  ^The\s+Maximum\s+Transmit\s+Unit\s+is\s+${MTU}
  ^Internet\s+Address\s+is\s+${IP_ADDRESS}/${IP_ADDRESS_MASK}
  ^${SPEED},\s+${DUPLEX},\s+link\s+type\s+is\s+${LINK_TYPE}
  ^\s*Last\s+clearing\s+of\s+counters:\s+${CLEAR_COUNTERS} -> Counters_raw_1

Counters_raw_1
  #     Last 300 seconds input rate 291.56 bytes/sec, 2332 bits/sec, 4.54 packets/sec
  #     Last 300 seconds input rate: 71 bytes/sec, 568 bits/sec, 0 packets/sec
  ^\s+Last\s+300\s+seconds\s+input\s+rate(:)?\s+${LAST_300_SEC_INPUT_RATE_BYTES}\s+bytes/sec,\s+${LAST_300_SEC_INPUT_RATE_BITS}\s+bits/sec,\s+${LAST_300_SEC_INPUT_RATE_PACKETS}\s+packets/sec
  #     Last 300 seconds output rate 269.08 bytes/sec, 2152 bits/sec, 3.99 packets/sec
  #     Last 300 seconds output rate: 625 bytes/sec, 5000 bits/sec, 2 packets/sec
  ^\s+Last\s+300\s+seconds\s+output\s+rate(:)?\s+${LAST_300_SEC_OUTPUT_RATE_BYTES}\s+bytes/sec,\s+${LAST_300_SEC_OUTPUT_RATE_BITS}\s+bits/sec,\s+${LAST_300_SEC_OUTPUT_RATE_PACKETS}\s+packets/sec
  #     Input: 46680717 packets, 1138745763 bytes, 46680717 buffers
  ^\s+Input:\s+${INPUT_PACKETS}\s+packets,\s+${INPUT_BTYPES}\s+bytes,\s+${INPUT_BUFFERS}\s+buffers -> Input_counters_raw_1
  #     Input: 2292 packets, 13332 bytes
  ^\s+Input:\s+${INPUT_PACKETS}\s+packets,\s+${INPUT_BTYPES}\s+bytes -> Input_counters_2_raw_1
  #     Output:34829332 packets, 3630673018 bytes, 34829332 buffers
  ^\s+Output:${OUTPUT_PACKETS}\s+packets,\s+${OUTPUT_BTYPES}\s+bytes,\s+${OUTPUT_BUFFERS}\s+buffers -> Output_counters_raw_1
  #     Output:16504 packets, 192131 bytes
  ^\s+Output:${OUTPUT_PACKETS}\s+packets,\s+${OUTPUT_BTYPES}\s+bytes -> Output_counters_2_raw_1
  ^\s*$$ -> Start 
  ^.*\s+current\s+state -> Error


Input_counters_raw_1
  # 397898 broadcasts, 10591248 multicasts, 0 pauses
  # 0 errors, 0 runts, 0 giants
  # 0 crc, 0 align errors, 0 overruns
  # 0 dribbles, 0 drops, 0 no buffers
  ^\s+${INPUT_BROADCASTS}\s+broadcasts,\s+${INPUT_MULTICASTS}\s+multicasts,\s+${INPUT_PAUSES}\s+pauses
  ^\s+${INPUT_ERRORS}\s+errors,\s+${INPUT_RUNTS}\s+runts,\s+${INPUT_GIANTS}\s+giants
  ^\s+${INPUT_CRC}\s+crc,\s+${INPUT_ALIGN_ERRORS}\s+align\s+errors,\s+${INPUT_OVERRUNS}\s+overruns
  ^\s+${INPUT_DRIBBLES}\s+dribbles,\s+${INPUT_DROPS}\s+drops,\s+${INPUT_NO_BUFFERS}\s+no\s+buffers -> Counters_raw_1
  ^.* -> Error

Output_counters_raw_1
  # 5 broadcasts, 1020098 multicasts, 0 pauses
  # 0 errors, 0 underruns, 0 collisions
  # 0 deferred, 0 lost carriers
  ^\s+${OUTPUT_BROADCASTS}\s+broadcasts,\s+${OUTPUT_MULTICASTS}\s+multicasts,\s+${OUTPUT_PAUSES}\s+pauses
  ^\s+${OUTPUT_ERRORS}\s+errors,\s+${OUTPUT_UNDERRUNS}\s+underruns,\s+${OUTPUT_COLLISIONS}\s+collisions
  ^\s+${OUTPUT_DEFERRED}\s+deferred,\s+${OUTPUT_LOST_CARRIERS}\s+lost\s+carriers
  ^\s*$$ -> Start
  ^.* -> Error

Input_counters_2_raw_1
  # 0 broadcasts, 0 multicasts
  # 0 errors, 0 runts, 0 giants
  # 0 CRC, 0 align errors, 0 overruns
  # 0 dribbles, 0 aborts, 0 no buffers
  # 0 frame errors
  ^\s+${INPUT_BROADCASTS}\s+broadcasts,\s+${INPUT_MULTICASTS}\s+multicasts
  ^\s+${INPUT_ERRORS}\s+errors,\s+${INPUT_RUNTS}\s+runts,\s+${INPUT_GIANTS}\s+giants
  ^\s+${INPUT_CRC}\s+CRC,\s+${INPUT_ALIGN_ERRORS}\s+align\s+errors,\s+${INPUT_OVERRUNS}\s+overruns
  ^\s+${INPUT_DRIBBLES}\s+dribbles,\s+${INPUT_DROPS}\s+aborts,\s+${INPUT_NO_BUFFERS}\s+no\s+buffers
  ^\s+${INPUT_FRAME_ERRORS}\s+frame\s+errors -> Counters_raw_1
  ^.* -> Error

Output_counters_2_raw_1
  # 0 errors, 0 underruns, 0 collisions
  # 0 deferred
  ^\s+${OUTPUT_ERRORS}\s+errors,\s+${OUTPUT_UNDERRUNS}\s+underruns,\s+${OUTPUT_COLLISIONS}\s+collisions
  ^\s+${OUTPUT_DEFERRED}\s+deferred
  ^\s+DCD
  ^\s*$$ -> Start
  ^.* -> Error

Raw_2
  ^Line\s+protocol\s+state:\s+${PROTOCOL_STATE}
  ^Description:\s+${DESCRIPTION}
  ^Bandwidth:\s+${BANDWIDTH}\s+kbps
  ^Maximum\s+transmission\s+unit:\s+${MTU}
  ^Internet\s+address:\s+${IP_ADDRESS}/${IP_ADDRESS_MASK}
  ^${SPEED}-speed\s+mode,\s+${DUPLEX}\s+mode
  ^Link\s+speed\s+type\s+is\s+${LINK_TYPE}
  ^Last\s+clearing\s+of\s+counters:\s+${CLEAR_COUNTERS} -> Counters_raw_2
  ^\s*$$ -> Start
  ^Current\s+state -> Error

Counters_raw_2
  #  Last 300 second input: 6 packets/sec 663 bytes/sec 0%
  # Last 300 seconds input:  653795 packets/sec 620328920 bytes/sec 3%
  ^\s?Last\s+300\s+second(s)?\s+input:\s+${LAST_300_SEC_INPUT_RATE_PACKETS}\s+packets/sec\s+${LAST_300_SEC_INPUT_RATE_BYTES}\s+bytes/sec
  # Last 300 seconds input rate: 124 bytes/sec, 992 bits/sec, 1 packets/sec
  ^Last\s+300\s+seconds\s+input\s+rate:\s+${LAST_300_SEC_INPUT_RATE_BYTES}\s+bytes/sec,\s+${LAST_300_SEC_INPUT_RATE_BITS}\s+bits/sec,\s+${LAST_300_SEC_INPUT_RATE_PACKETS}\s+packets/sec
  #  Last 300 second output: 6 packets/sec 603 bytes/sec 0%
  # Last 300 seconds output:  752413 packets/sec 767574527 bytes/sec 3%
  ^\s?Last\s+300\s+second(s)?\s+output:\s+${LAST_300_SEC_OUTPUT_RATE_PACKETS}\s+packets/sec\s+${LAST_300_SEC_OUTPUT_RATE_BYTES}\s+bytes/sec
  # Last 300 seconds output rate: 217 bytes/sec, 1736 bits/sec, 2 packets/sec
  ^Last\s+300\s+seconds\s+output\s+rate:\s+${LAST_300_SEC_OUTPUT_RATE_BYTES}\s+bytes/sec,\s+${LAST_300_SEC_OUTPUT_RATE_BITS}\s+bits/sec,\s+${LAST_300_SEC_OUTPUT_RATE_PACKETS}\s+packets/sec
  #  Input (total):  45433501 packets, 1512350682 bytes
  # Input (total):  1378318211088 packets, 1485686536611046 bytes
  ^\s?Input\s+\(total\):\s+${INPUT_PACKETS}\s+packets,\s+${INPUT_BTYPES}\s+bytes -> Input_counters_raw_2
  #  Output (total): 46828598 packets, 5275041371 bytes
  ^\s?Output\s+\(total\):\s+${OUTPUT_PACKETS}\s+packets,\s+${OUTPUT_BTYPES}\s+bytes -> Output_counters_raw_2
  # Input: 5273737 packets, 1258304991 bytes, 0 drops
  ^Input:\s+${INPUT_PACKETS}\s+packets,\s+${INPUT_BTYPES}\s+bytes,\s+${INPUT_DROPS}\s+drops
  # Output: 12301693 packets, 1773191423 bytes, 0 drops
  ^Output:\s+${OUTPUT_PACKETS}\s+packets,\s+${OUTPUT_BTYPES}\s+bytes,\s+\d+\s+drops
  ^\s*$$ -> Start

Input_counters_raw_2
  #         1378272321334 unicasts, 43421092 broadcasts, 2468662 multicasts, 0 pauses
  # Input (normal):  1378318204300 packets, - bytes
  #         1378272321256 unicasts, 43421092 broadcasts, 2468662 multicasts, 0 pauses
  # Input:  0 input errors, 0 runts, 0 giants, 0 throttles
  #         0 CRC, 0 frame, - overruns, 0 aborts
  #         - ignored, - parity errors
  ^\s+\d+\s+unicasts,\s+${INPUT_BROADCASTS}\s+broadcasts,\s+${INPUT_MULTICASTS}\s+multicasts,\s+${INPUT_PAUSES}\s+pauses
  ^\s+${INPUT_CRC}\s+CRC,\s+${INPUT_FRAME_ERRORS}\s+frame,\s+${INPUT_OVERRUNS}\s+overruns,\s+${INPUT_DROPS}\s+aborts
  ^.*ignored -> Counters_raw_2
  ^\s.*unicasts
  ^\s*Input
  ^.* -> Error

Output_counters_raw_2
  #           - unicasts, - broadcasts, - multicasts, - pauses
  # Output (normal): 46828598 packets, 5275041371 bytes
  #         45586840 unicasts, 4 broadcasts, 1241754 multicasts, 0 pauses
  # Output: 0 output errors, - underruns, - buffer failures
  #         0 aborts, 0 deferred, 0 collisions, 0 late collisions
  #         - lost carrier, - no carrier
  ^\s+\d+\s+unicasts,\s+${OUTPUT_BROADCASTS}\s+broadcasts,\s+${OUTPUT_MULTICASTS}\s+multicasts,\s+${OUTPUT_PAUSES}\s+pauses
  ^\s+Output:\s+${OUTPUT_ERRORS}\s+output\s+errors,\s+${OUTPUT_UNDERRUNS}\s+underruns
  ^\s+\d+\s+aborts,\s+${OUTPUT_DEFERRED}\s+deferred,\s+${OUTPUT_COLLISIONS}\s+collisions
  ^\s+${OUTPUT_LOST_CARRIERS}\s+lost\s+carrier
  ^\s*$$ -> Start